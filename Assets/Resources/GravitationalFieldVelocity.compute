
#include "GravitationalField.cginc"

#pragma kernel ComputeVelocity

RWStructuredBuffer<FieldPoint> point_buffer;
RWStructuredBuffer<Body>       body_buffer;

int   margin;
float delta_time;

float3 Bilerp(float3 p_0, float3 p_1, float3 p_2, float3 p_3, float x, float y)
{
    return lerp(lerp(p_0, p_1, x),
                lerp(p_2, p_3, x), y);
}

float3 Trilerp(float3 p_0, float3 p_1, float3 p_2, float3 p_3,
               float3 p_4, float3 p_5, float3 p_6, float3 p_7, float x, float y, float z)
{
    return lerp(Bilerp(p_0, p_1, p_2, p_3, x, y),
                Bilerp(p_4, p_5, p_6, p_7, x, y), z);
}

uint IndexOffset(uint3 i, uint x, uint y, uint z)
{
    return Index(clamp(i + uint3(x, y, z), (uint3)0, uint3(w - 1, h - 1, d - 1)));
}

float3 Displacement(Body body, FieldPoint field_point)
{
    return field_point.displacement -
           GravitationalDisplacement(body, field_point);
}

float WrapComponent(float dimension, float component)
{
    float d = dimension * 0.5f + margin;
    
    if (component < -d)
        component = d - (-d - component);
    else
    if (component > d)
        component = -d + (component - d);

    return component;
}

[numthreads(1, 1, 1)]
void ComputeVelocity(uint id : SV_DispatchThreadID)
{
    Body   body     = body_buffer[id];
    float4 position = body.position;
    float3 velocity = body.velocity;
    float  mass     = body.mass;

    /*
           6             7
            +-----------+
           /.          /|
        2 / .       3 / |
         +-----------+  |
         |  .        |  |
         |4 . . . . .|. + 5
         | .         | /
         |.          |/
         +-----------+
        0             1
    */

    uint3 index = uint3(position.x + (w - 1) * 0.5f,
                        position.y + (h - 1) * 0.5f,
                        position.z + (d - 1) * 0.5f);

    float3 displacements[8];
    displacements[0] = Displacement(body, point_buffer[Index      (index)         ]);
    displacements[1] = Displacement(body, point_buffer[IndexOffset(index, 1, 0, 0)]);
    displacements[2] = Displacement(body, point_buffer[IndexOffset(index, 0, 1, 0)]);
    displacements[3] = Displacement(body, point_buffer[IndexOffset(index, 1, 1, 0)]);
    displacements[4] = Displacement(body, point_buffer[IndexOffset(index, 0, 0, 1)]);
    displacements[5] = Displacement(body, point_buffer[IndexOffset(index, 1, 0, 1)]);
    displacements[6] = Displacement(body, point_buffer[IndexOffset(index, 0, 1, 1)]);
    displacements[7] = Displacement(body, point_buffer[IndexOffset(index, 1, 1, 1)]);

    float3 displacement = Trilerp(displacements[0],
                                  displacements[1],
                                  displacements[2],
                                  displacements[3],
                                  displacements[4],
                                  displacements[5],
                                  displacements[6],
                                  displacements[7], frac(position.x),
                                                    frac(position.y),
                                                    frac(position.z));

    if (mass)
        displacement /= mass;
    velocity     += displacement;
    position.xyz += velocity * delta_time;

    position.x = WrapComponent(w, position.x);
    position.y = WrapComponent(h, position.y);
    position.z = WrapComponent(d, position.z);

    body_buffer[id].position = position;
    body_buffer[id].velocity = velocity;
}
